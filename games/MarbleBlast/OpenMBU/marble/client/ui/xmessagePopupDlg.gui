//--- OBJECT WRITE BEGIN ---
new GuiControl(XMessagePopupDlg) {
   profile = "GuiDefaultProfile";
   horizSizing = "width";
   vertSizing = "height";
   position = "0 0";
   extent = "640 480";
   minExtent = "8 8";
   visible = "1";

   new GuiBitmapCtrl(XMessageBGFrame) {
     profile = "GuiDefaultProfile";
      alpha = "0";
	   horizSizing = "width";
	   vertSizing = "height";
   	position = "0 0";
	   extent = "640 480";
      bitmap = "./xbox/fade_black";
      wrap = "1";
	   visible = "1";
      
	   new GuiBitmapCtrl(XMessagePopFrame) {
	      profile = "GuiDefaultProfile";
	      horizSizing = "center";
	      vertSizing = "center";
	      position = "70 30";
	      extent =   "512 400"; 
	      minExtent = "48 92";
	      visible = "1";
	      bitmap = "./xbox/popupGUI";
	      wrap = "0";
	            
	      new GuiMLTextCtrl(XMessagePopupText) {
	         profile = "TextHeadingSmallNoShadeProfile";
				position = "103 85";
				extent =   "313 186";
	         minExtent = "8 8";
	         visible = "1";
	         lineSpacing = "2";
	         allowColorChars = "0";
	         maxChars = "-1";
	         maxAlpha = "1.0";
	         minAlpha = "1.0";
	      };
	      
		   new GuiControl(XMessagePopupBottomNavBarNew) {
		      profile = "GuiDefaultProfile";
		      horizSizing = "right";
		      vertSizing = "bottom";
		      position = "-119 248";
		      extent = "640 200";
		      minExtent = "8 2";
		      visible = "1";
		      UIMode = "NewOnly";
		      
		      new GuiXboxButtonCtrl(XMessagePopupABtnNew) {
               profile = "TextMenuButtonProfile";
               horizSizing = "right";
               vertSizing = "bottom";
               position = "330 0";
               extent = "120 94";
               minExtent = "8 2";
               bitmap = "./xbox/pad_button_a";
               bitmapAlign = "right";
               text = "";
               command = "XMessagePopupDlg.onA(); XMessagePopupDlg.onAReleased();";
               visible = "1";
            };
		      
		      new GuiXboxButtonCtrl(XMessagePopupBBtnNew) {
               profile = "TextMenuButtonProfile";
               horizSizing = "right";
               vertSizing = "bottom";
               position = "440 0";
               extent = "120 94";
               minExtent = "8 2";
               bitmap = "./xbox/pad_button_b";
               bitmapAlign = "right";
               text = "";
               command = "XMessagePopupDlg.onB(); XMessagePopupDlg.onBReleased();";
               visible = "1";
            };
		      
		   };
	      
		   new GuiControl(XMessagePopupBottomNavBar) {
		      profile = "GuiDefaultProfile";
		      horizSizing = "right";
		      vertSizing = "bottom";
		      position = "-119 248";
		      extent = "640 200";
		      minExtent = "8 2";
		      visible = "1";
		      UIMode = "LegacyOnly";
		
		      new GuiControl(XMessagePopupABtn) {
		         profile = "GuiDefaultProfile";
		         horizSizing = "right";
		         vertSizing = "bottom";
		         position = "34 40";
		         extent = "500 60";
		         minExtent = "8 2";
		         bitmap = "./xbox/roundedBG";
		         visible = "1";
		
		         new GuiBitmapCtrl(XMessagePopupAButtonImage) {
		            profile = "GuiDefaultProfile";
		            horizSizing = "right";
		            vertSizing = "bottom";
		            position = "440 0";
		            extent = "45 45";
		            minExtent = "8 2";
		            visible = "1";
		            bitmap = "./xbox/pad_button_a";
		            wrap = "0";
		            onMouseUp = "XMessagePopupDlg.onA(); XMessagePopupDlg.onAReleased();";
		         };
		         new GuiTextCtrl(XMessagePopupABtnTxt) {
		            profile = "TextAButtonProfile";
		            horizSizing = "right";
		            vertSizing = "bottom";
		            position = "4 5";
		            extent = "437 34";
		            minExtent = "50 34";
		            visible = "1";
		            text = "";
		            maxLength = "255";
		         };
		      };
		      new GuiControl(XMessagePopupBBtn) {
		         profile = "GuiDefaultProfile";
		         horizSizing = "right";
		         vertSizing = "bottom";
		         position = "67 5";
		         extent = "500 60";
		         minExtent = "8 2";
		         bitmap = "./xbox/roundedBG";
		         visible = "1";
		         
		         new GuiBitmapCtrl(XMessagePopupBButtonImage) {
		            profile = "GuiDefaultProfile";
		            horizSizing = "right";
		            vertSizing = "bottom";
		            position = "440 5";
		            extent = "35 35";
		            minExtent = "8 2";
		            visible = "1";
		            bitmap = "./xbox/pad_smallbutton_b";
		            wrap = "0";
		            onMouseUp = "XMessagePopupDlg.onB(); XMessagePopupDlg.onBReleased();";
		         };
		         new GuiTextCtrl(XMessagePopupBBtnTxt) {
		            profile = "TextBButtonProfile";
		            horizSizing = "right";
		            vertSizing = "bottom";
		            position = "4 5";
		            extent = "437 34";
		            minExtent = "50 34";
		            visible = "1";
		            text = "";
		            maxLength = "255";
		         };
		      };
			};
		};
	};
};
//--- OBJECT WRITE END ---

function XMessagePopupDlg::onA(%this)
{
   %this.adown = true;
}

function XMessagePopupDlg::onAReleased(%this)
{
   if (XMessagePopupABtn.visible && %this.adown)
   {      
      sfxPlay( AudioButtonDown );
      %callback = %this.stackData[%this.stack].A_callback;
      %this.hide();
      if (%callback !$= "")
         eval(%callback);
   }
   %this.adown = false;
}

function XMessagePopupDlg::onB(%this)
{
   %this.bdown = true;
}

function XMessagePopupDlg::onBReleased(%this)
{
   if (XMessagePopupBBtn.visible && %this.bdown)
   {
      sfxPlay( AudioButtonDown );
      %callback = %this.stackData[%this.stack].B_callback;
      %this.hide();
      if (%callback !$= "")
         eval(%callback);
   }
   %this.bdown = false;
}

function XMessagePopupDlg::pushCurrentContent(%this)
{
   if (%this.stack $= "")
      %this.stack = 0;
 
   if (%this.stack > 9) // arbitrary limit of 9 concurrent dialogs
      error("XMessagePopupDlg: dialog stack overflow");
   else
      %this.stack++;
}

function XMessagePopupDlg::popCurrentContent(%this)
{
   if (%this.stack > 0)
   {
      %this.stackData[%this.stack].delete();
      %this.stack--;    
   }
}

function XMessagePopupDlg::setCurrentContent(%this)
{
   if (%this.stack < 1)
      return;
      
   if (%this.stackData[%this.stack].text !$= "")
      XMessagePopupText.setText(%this.stackData[%this.stack].text);
   else
      XMessagePopupText.setText("");
      
   if (%this.stackData[%this.stack].A_button_text !$= "")
   {
      XMessagePopupABtnTxt.setText(%this.stackData[%this.stack].A_button_text);
      XMessagePopupABtn.setVisible(true);
      
      XMessagePopupABtnNew.setText(%this.stackData[%this.stack].A_button_text);
      XMessagePopupABtnNew.setVisible(true);
   }
   else
   {
      XMessagePopupABtnTxt.setText("");
      XMessagePopupABtn.setVisible(false);
      
      XMessagePopupABtnNew.setText("");
      XMessagePopupABtnNew.setVisible(false);
   }
   
   if (%this.stackData[%this.stack].B_button_text !$= "")
   {
      XMessagePopupBBtnTxt.setText(%this.stackData[%this.stack].B_button_text);
      XMessagePopupBBtn.setVisible(true); 
        
      XMessagePopupBBtnNew.setText(%this.stackData[%this.stack].B_button_text);
      XMessagePopupBBtnNew.setVisible(true);  
   }
   else
   {
      XMessagePopupBBtnTxt.setText("");
      XMessagePopupBBtn.setVisible(false);   
      
      XMessagePopupBBtnNew.setText("");
      XMessagePopupBBtnNew.setVisible(false);
   }
}

function XMessagePopupDlg::show(%this, %priority, %txt, %A_button_text, %A_callback, %B_button_text, %B_callback)
{
   // if the new content is same as current content, don't push it again
   if (%this.stack > 0 && %txt $= %this.stackData[%this.stack].text)
      return;
   
   // if priority is nonzero, see if there is already an object with the priority level; if so, we'll just replace
   // it
   %stackObj = 0;
   if (%priority > 0)
      %stackObj = %this.findObjectByPriority(%priority);
      
   // if we have no stack object, we'll need to make a new one
   if (!isObject(%stackObj))
   {
      %this.pushCurrentContent();
      // watch out for the friggin instant group - we don't want our dialogs to get deleted from under us!
      // (e.g., in pushing dialogs in Lobby)
      %saveInstantGroup = $InstantGroup;
      $InstantGroup = "";
      %stackObj = new ScriptObject();
      $InstantGroup = %saveInstantGroup;
      %this.stackData[%this.stack] = %stackObj;
   }
   
   %stackObj.text = %txt;
   %stackObj.A_button_text = %A_button_text;
   %stackObj.A_callback = %A_callback;
   %stackObj.B_button_text = %B_button_text;
   %stackObj.B_callback = %B_callback;
   %stackObj.priority = %priority;
      
   %this.sortStack();
               
   if (!%this.isAwake())
   {
      Canvas.pushDialog(XMessagePopupDlg, 10);   
      
//	   XMessageBGFrame.alpha = 0;
//	   XMessageBGFrame.animateAlpha(0.7, 75, 300);
   }
}

function XMessagePopupDlg::sortStack(%this)
{
   // sort by priority using...bubble sort!
   
   for (%i = %this.stack; %i > 0; %i--)
   {
      for (%j = 2; %j <= %i; %j++)
      {
         if (%this.stackData[%j-1].priority > %this.stackData[%j].priority)
         {
            %temp = %this.stackData[%j-1];
            %this.stackData[%j-1] = %this.stackData[%j];
            %this.stackData[%j] = %temp;
         }
      }
   }
   
   // reset current content since we moved stuff around
   %this.setCurrentContent();
}

function XMessagePopupDlg::findObjectByPriority(%this, %priority)
{
   %obj = "";
   // search from top to bottom.  stop when we run out of things to search for or when we hit an object
   // with zero priority (since all objects below it in the stack will also have zero priority)
   for (%i = %this.stack; %i > 0 && %this.stackData[%i].priority !$= 0; %i--)
      if (%this.stackData[%i].priority == %priority)
         %obj = %this.stackData[%i];
   return %obj;
}

function XMessagePopupDlg::hide(%this)
{
   // if we don't have a message popup, no need to do anything
   if (!%this.isAwake())
      return;

   %this.popCurrentContent();
   
   if (%this.stack > 0)
      %this.setCurrentContent();
   else
      Canvas.popDialog(%this);    
}

function XMessagePopupDlg::remove(%this)
{
   // eventually we should go away
   while (%this.isAwake())
      %this.hide(); 
}

function xmpd_tm1()
{
   XMessagePopupDlg.show(0, "TEST MESSAGE", "CONTINUE", "", "CANCEL", "");
}

function xmpd_tm2()
{
   XMessagePopupDlg.show(0, "TEST MESSAGE2", "CONTINUE", "", "CANCEL", "");
}

function xmpd_tm3()
{
   XMessagePopupDlg.show(0, "TEST MESSAGE2", "CONTINUE", "", "CANCEL", "");
}

function xmpd_test()
{
   XMessagePopupDlg.hide();
   XMessagePopupDlg.delete();
   exec("marble/client/ui/xmessagepopupdlg.gui");
   $xmpd = XMessagePopupDlg;
   XMessagePopupDlg.show(0, "This is a X very very very very very very very very very very very very very very very very very very very very very very very long string", "CONTINUE", "", "CANCEL", "");
   //XMessagePopupDlg.show(0, "x x x x x x x x x x x x x x x x x x x x x x x x x x x x x x x x x x x x x x x x x x x x x x x x x x x x x x x x x x x x x x x x x x x x x x x x x x x x x x x x x x x x ", "CONTINUE", "", "CANCEL", "");
}