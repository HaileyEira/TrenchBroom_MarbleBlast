//--- OBJECT WRITE BEGIN ---
new GuiControl(DifficultySelectGui) {
   profile = "GuiDefaultProfile";
   horizSizing = "width";
   vertSizing = "height";
   position = "0 0";
   extent = "640 480";
   minExtent = "8 8";
   visible = "1";

   new GuiXboxListCtrl(DifficultyMenu) {
      profile = isWidescreen()? "TextMenuListProfile" : "TextMenuListSmallProfile";
	   horizSizing = "left";
	   vertSizing = "height";
		position = isWidescreen()? "60 -25" : "290 -30"; // will autocenter vertically
      extent = "640 480";
   };
};
//--- OBJECT WRITE END ---
function DifficultySelectGui::onA(%this)
{
   sfxPlay( AudioButtonDown );
   
   if (!%this.menuEnabled)
      return true;
      
   $Game::SPGemHunt = false;
     
   switch$ ( DifficultyMenu.getSelectedData() )
   {
      case 0:
         GameMissionInfo.setMode(GameMissionInfo.SPMode);
         GameMissionInfo.setFilterDifficulty( beginner );
         RootGui.setContent(LevelPreviewGui);
      case 1: 
         GameMissionInfo.setMode(GameMissionInfo.SPMode);
         GameMissionInfo.setFilterDifficulty( intermediate );
         RootGui.setContent(LevelPreviewGui);
      case 2: 
         GameMissionInfo.setMode(GameMissionInfo.SPMode);
         GameMissionInfo.setFilterDifficulty( advanced );
         RootGui.setContent(LevelPreviewGui);
      case 3: 
         GameMissionInfo.setMode(GameMissionInfo.CustomMode);
         GameMissionInfo.setFilterDifficulty( custom );
         RootGui.setContent(LevelPreviewGui);
      case 4: 
         GameMissionInfo.setMode(GameMissionInfo.SPMode);
         $Game::SPGemHunt = true;
         $Game::SPGemHuntSeeded = true;
         $Game::SPGemHuntSeed = 100;
         RootGui.setContent(LevelPreviewGui);
      case 5: 
         GameMissionInfo.setMode(GameMissionInfo.SPMode);
         $Game::SPGemHunt = true;
         $Game::SPGemHuntSeeded = false;
         RootGui.setContent(LevelPreviewGui);
   }
      
   return true;
}

function DifficultySelectGui::onB(%this)
{
   sfxPlay( AudioButtonDown );
   RootGui.setContent(MainMenuGui);
   return true;
}

function DifficultySelectGui::show(%this, %fromGui)
{
   %this.menuEnabled = true;
   
   %hasCustom = CustomSinglePlayMissionGroup.getCount() > 0;
   
   DifficultyMenu.clear();
   DifficultyMenu.addRow($Text::LevelsBeginner, 0, 0, 2);
   DifficultyMenu.addRow($Text::LevelsIntermediate, 1, 0, 2);
   if (%hasCustom)
      DifficultyMenu.addRow($Text::LevelsAdvanced, 2, 0, 2);
   else
      DifficultyMenu.addRow($Text::LevelsAdvanced, 2, 20, 2);
   if (%hasCustom)
      DifficultyMenu.addRow($Text::LevelsCustom, 3, 20, 2);
   DifficultyMenu.addRow($Text::SPGemHunt SPC "(Seeded)", 4, 0, 2);
   DifficultyMenu.addRow($Text::SPGemHunt SPC "(Random)", 5, 0, 2);
                
   RootGui.setTitle($Text::DifficultyMenu);
   if ($pref::UI::LegacyUI)
      RootGui.setA($Text::GO);
   RootGui.setB($Text::Back);
}
